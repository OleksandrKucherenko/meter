// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    /* GLOBAL REPOSITORIES FOR BUILD SCRIPT, DEPENDENCIES SERVERS */
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
    }
    /* REFERENCES ON BUILD PLUGINS */
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:+'
    }
}

/* Enable/Disable PreDex step during the build on CIS. */
project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

// get Git revision as a NUMBER (subversion style of revision)
def gitRevision() {
    return "git rev-list --count HEAD".execute().text.trim();
}

/* INJECT IN ALL PROJECT GLOBAL SETTINGS */
allprojects {
    /* DEFINE REPOSITORIES FOR SUB-PROJECTS */
    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        /* ATTACH CUSTOM BUILDS OF GRADLE PLUGINS FROM '~/gradle/plugins' folder */
//        flatDir { dirs rootProject.projectDir.toString() + '/gradle/plugins' }
    }

    /* DEFINE GLOBAL VARIABLES */
    ext {
        androidCompileSdkVersion = Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
        androidBuildToolsVersion = project.ANDROID_BUILD_TOOLS_VERSION /* latest: "20.0.0" */

        /* specify versions of 3rd party libs to use in project */
        androidSupportLib = project.ANDROID_SUPPORT_LIB_VERSION /* latest: v21.0.0-rc1 is only for Android-L */

        /* MIN and TARGET SDKs for our binaries */
        androidTargetSdkVersion = Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        androidMinSdkVersion = Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)

        /* DEFINE SHOULD TESTING CONFIGURATION BE ENABLED OR NOT */
        useTesting = Boolean.parseBoolean(project.USE_TDD)

        /* SHOULD WE USE ESPRESSO FRAMEWORK OR NOT */
        useEspresso = useTesting & Boolean.parseBoolean(project.USE_ESPRESSO)

        /* DEFINE SHOULD WE USE ROBOLECTRIC TESTS OR NOT */
        useRobolectric = useTesting & Boolean.parseBoolean(project.USE_ROBOLECTRIC)

        /* CODE COVERAGE PLUGIN/TOOL VERSION */
        JaCoCoVersion = project.JACOCO_VERSION
        CoverallsVersion = project.COVERALLS_VERSION

        /* dynamic calculation of git revision, its a part of the app version now */
        GIT_REVISION = "${gitRevision()}"
    }

    /* ATTACH DEPENDENCY UPDATE VERSION REPORT PLUGIN */
    apply plugin: 'com.github.ben-manes.versions'
}

/* PRINT to output configuration details of the build. */
println new String(new char[80]).replace("\0", "-")
println 'Android Build Tools: ' + project.ANDROID_BUILD_TOOLS_VERSION
println 'Android SDK version: ' + project.ANDROID_BUILD_SDK_VERSION
println 'Android Support Library: ' + project.ANDROID_SUPPORT_LIB_VERSION
println 'Target API: ' + project.ANDROID_BUILD_TARGET_SDK_VERSION
println 'Minimal API: ' + project.ANDROID_BUILD_MIN_SDK_VERSION
println 'Use Tests: ' + project.USE_TDD
println 'Use Espresso: ' + project.USE_ESPRESSO
println 'Use Robolectric: ' + project.USE_ROBOLECTRIC
println 'JaCoCo Library: ' + project.JACOCO_VERSION
println 'Coveralls Version: ' + project.COVERALLS_VERSION
println 'Source code GIT Revision: ' + "${gitRevision()}"
println new String(new char[80]).replace("\0", "-")

// References:
// http://toastdroid.com/2014/03/28/customizing-your-build-with-gradle/
// http://stackoverflow.com/questions/18328730/how-to-create-a-release-signed-apk-file-using-gradle/19603006#19603006
// http://stackoverflow.com/questions/20541630/robolectric-does-not-support-api-level
// http://raptordigital.blogspot.se/2014/06/robolectric-plugin-upgraded-to-011x.html
//
// Useful Commands:
// [1] Shows dependencies of projects in tree view representation
// > gradle projects -q
//
// [2] Show all tasks
// > gradle tasks --all
//
// [3] get dependency update report:
// > gradle dependencyUpdates
// > gradle dependencyUpdates -Drevision=release
//
// [4] profile build performance:
// > gradle clean assembleDebug --profile
